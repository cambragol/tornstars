//*******************************************************************************************/
//
// tPogClass
//
// Similar to C++ sim classes, these pog classes allow objects and code behavior to be
// merged into one.
// 
//
// August 2003, Jon Freise
//******************************************************************************************/

package tPogClass;

uses    Sim,
        iShip,
        iSim,
        Debug,
        Global,
        GUI,
        iGUI,
        iHUD,
        INIFile,
        Input,
        List,
        Object,
        String,
        Task,
        Text,
        tPcFenceShip,
        tPcAsteroidBelt,
        tPcConstructionShip,
        tPcCustomsToll,
        tPcPirateToll,
        tPcGuards,
        tPcHiredShip,
        tPcOwner,
        tPcPirateToll,
        tPcPodScrapper,
        tPcRumorSeller,
        tPcTankerShip,
        tPcWeaponSeller,
        tPcEmbezzleOp,
        tPcWreckBelt,
        tPcCargoShip;


provides Create,
         Start;


prototype PogCreate();
prototype task PogStart();


/* Example minimum pog class file instance.

[PogClass]
name=test_class

[Properties]
template=ini:/sims/stations/custom/ice_asteroid_debris

create_function=pog_package.create

start_function=pog_package.start


   Resulting Object minimum properties
   
   "name" 
   "gt_pog_object_id"
   "gt_pog_object_start"


*/

/* 

  Takes a pog object ini file and creates an instance of the object.
  (The object should then be placed into the world, and started.)
 
  Template_name is the class instance template file. It allows
  building custom versions of the object.
  
  Object_name is just a referencing string. It could be used by
  the class to allow its object to be found by other objects. For 
  instance, a patrol path object might name each subwaypoint Name1, Name2
  etc. This allows patrol objects to find the path and follow it.
  
  Id_String is seperate from Object_name to allow a unique id string to be handed
  to a new object. This is useful for objects that must preserve state between
  player visits. For instance, a dead ship object might give cargo when a 
  player docks, but only once. If the graveyard gives this ship a unique
  id string, it can create a global that remembers if it has been visited.
  By seperating the id from the name, the same localized name can be used
  for many, many dead ship objects without global clash. 

   The create function is responsible for reading the INI file supplied,
   and using it to modify any properties the object will need later 
   at run time.   
 */

hsim Create(string template_name, string object_name)
{
   hinifile object_file = INIFile.Create(template_name);
   string pog_class, pog_function, create_function;
   hsim item = none;
   htask current_task, new_task;

   if (none == object_file) {
      debug Debug.PrintString("tPogClass: Error, no template file\n");
      debug Debug.PrintString(template_name);
      debug Debug.PrintString("\n");
      return none;

   }

   pog_function = INIFile.String(object_file,"PogClass","name","none");   

   INIFile.Destroy(object_file);

   if ("tPcFenceShip" == pog_function){
      item = iSim.Cast(tPcFenceShip.PogCreate(template_name, object_name));
   }else if ("tPcAsteroidBelt" == pog_function){
      item = iSim.Cast(tPcAsteroidBelt.PogCreate(template_name, object_name));
   }else if ("tPcWreckBelt" == pog_function){
      item = iSim.Cast(tPcWreckBelt.PogCreate(template_name, object_name));
   }else if ("tPcConstructionShip" == pog_function){
      item = iSim.Cast(tPcConstructionShip.PogCreate(template_name, object_name));
   }else if ("tPcCustomsToll" == pog_function){
      item = iSim.Cast(tPcCustomsToll.PogCreate(template_name, object_name));
   }else if ("tPcGuards" == pog_function){
      item = iSim.Cast(tPcGuards.PogCreate(template_name, object_name));
   }else if ("tPcHiredShip" == pog_function){
      item = iSim.Cast(tPcHiredShip.PogCreate(template_name, object_name));
   }else if ("tPcPirateToll" == pog_function){
      item = iSim.Cast(tPcPirateToll.PogCreate(template_name, object_name));
   }else if ("tPcPodScrapper" == pog_function){
      item = iSim.Cast(tPcPodScrapper.PogCreate(template_name, object_name));
   }else if ("tPcRumorSeller" == pog_function){
      item = iSim.Cast(tPcRumorSeller.PogCreate(template_name, object_name));
   }else if ("tPcTankerShip" == pog_function){
      item = iSim.Cast(tPcTankerShip.PogCreate(template_name, object_name));
   }else if ("tPcWeaponSeller" == pog_function){
      item = iSim.Cast(tPcWeaponSeller.PogCreate(template_name, object_name));
   }else if ("tPcEmbezzleOp" == pog_function){
      item = iSim.Cast(tPcEmbezzleOp.PogCreate(template_name, object_name));
   }else if ("tPcCargoShip" == pog_function){
      item = iSim.Cast(tPcCargoShip.PogCreate(template_name, object_name));
   }else if ("none" == pog_function){
      item = Sim.Create(template_name, object_name);
   }else {
      item = none;

      debug {
         Debug.PrintString("tPogClass.Create Error pog class invalid\n");
         Debug.PrintString(pog_function);
         Debug.PrintString("\n");
      }
   }

   return item;
}

/* 
   Start the behavior task for the specified pog object. This is seperated from the 
   create to allow the creating task to place the object, set faction, or adjust
   properties before the object code takes over.

   The create function must set the start function. At this point, all properties
   are set and ready to run. The object is placed, and is ready for code
   execution.  
 
 */
Start(hsim pog_object)
{
   string pog_start;
   htask new_task;

   pog_start = Object.StringProperty(pog_object, "gt_pog_object_class");

   if ("tPcFenceShip" == pog_start){
      Task.Detach(start tPcFenceShip.PogStart(pog_object));
   }else if ("tPcAsteroidBelt" == pog_start){
      Task.Detach(start tPcAsteroidBelt.PogStart(pog_object));
   }else if ("tPcWreckBelt" == pog_start){
      Task.Detach(start tPcWreckBelt.PogStart(pog_object));
   }else if ("tPcConstructionShip" == pog_start){
      Task.Detach(start tPcConstructionShip.PogStart(pog_object));
   }else if ("tPcCustomsToll" == pog_start){
      Task.Detach(start tPcCustomsToll.PogStart(pog_object));
   }else if ("tPcGuards" == pog_start){
      Task.Detach(start tPcGuards.PogStart(pog_object));
   }else if ("tPcHiredShip" == pog_start){
      Task.Detach(start tPcHiredShip.PogStart(pog_object));
   }else if ("tPcPirateToll" == pog_start){
      Task.Detach(start tPcPirateToll.PogStart(pog_object));
   }else if ("tPcPodScrapper" == pog_start){
      Task.Detach(start tPcPodScrapper.PogStart(pog_object));
   }else if ("tPcRumorSeller" == pog_start){
      Task.Detach(start tPcRumorSeller.PogStart(pog_object));
   }else if ("tPcTankerShip" == pog_start){
      Task.Detach(start tPcTankerShip.PogStart(pog_object));
   }else if ("tPcWeaponSeller" == pog_start){
      Task.Detach(start tPcWeaponSeller.PogStart(pog_object));
   }else if ("tPcEmbezzleOp" == pog_start){
      Task.Detach(start tPcEmbezzleOp.PogStart(pog_object));
   }else if ("tPcCargoShip" == pog_start){
      Task.Detach(start tPcCargoShip.PogStart(pog_object));
   }else {
      debug {
         Debug.PrintString("tPogClass.Start Error pog class invalid\n");
         Debug.PrintString(pog_start);
         Debug.PrintString("\n");
      }
   }
}


/*
   Test code just looks up a sim type from the properties section of 
   the class instance INI file. It creates that Sim and passes it 
   back.

*/
PogCreate()
{

   hinifile object_file;
   string name, file_name;
   string item_template, pog_class, start_function;
   hsim item;
   htask current_task;
   htask parent_task;

   debug Debug.PrintString("PogClass.PogCreate\n");

   current_task = Task.Current();

   parent_task = Task.Cast(Object.HandleProperty(current_task,"gt_pc_object_parent"));

   file_name = Object.StringProperty(current_task,"gt_pc_object_file");
   name = Object.StringProperty(current_task,"gt_pc_object_name");

   object_file = INIFile.Create(file_name);

   if (none == object_file) {
      debug {
         Debug.PrintString("PogClass.PogCreate, failed to find object file\n");
      }

      return;
   }

   // Pull needed items out of template file. Create the object.
   // Attach them

   pog_class = INIFile.String(object_file, "PogClass","name","none");


   ///********************
   // This block of code is class dependent. The rest must be done to adhere
   // to the pog object calling conventions.

   item_template = INIFile.String(object_file, "Properties","template","none");
   item = Sim.Create(item_template, name);

   if (none == item) {
      debug {
         Debug.PrintString("PogClass.PogCreate, bad object template\n");
      }
      return;
   }
   ///********************


   // All 
   Object.AddStringProperty(item, "gt_pog_object_class", pog_class);
   Object.AddStringProperty(item, "gt_pog_object_template", file_name);

      
   // Attach the new item for return to the calling task.
   if (Object.PropertyExists(parent_task, "gt_pc_object")) {
      Object.SetHandleProperty(parent_task,"gt_pc_object", item);
   }else {
      Object.AddHandleProperty(parent_task,"gt_pc_object", item);
   }

   Task.Resume(parent_task);
}


/*
   A simple behavior test.

*/
task PogStart()
{
   hship player;
   float distance;
   hsim pog_object;

   debug Debug.PrintString("PogClass.PogStart: alive\n");

   pog_object = Sim.Cast(Object.HandleProperty(Task.Current(),"gt_pc_object"));


   ///***************************************
   // This part of the start code is object dependent. The prior pieces must
   // be done to adhere to the pog class calling convention.

   while (1) {
      Task.Sleep(Task.Current(), 10.0);

      // skip out if we get culled.
      if (!Sim.IsAlive(pog_object)) {
         return;
      }

      player = iShip.FindPlayerShip();
      distance = Sim.DistanceBetween(player, pog_object);

      iHUD.Print(String.Join("Distance ",String.FromFloat(distance)));
   }

   ///***************************************
}

/*   

THIS MOD IS NOT MADE, DISTRIBUTED, OR SUPPORTED BY INFOGRAMES
OR PARTICLE SYSTEMS LTD. 

ELEMENTS TM & (C) INFOGRAMES AND PARTICLE SYSTEMS LTD.
*/
