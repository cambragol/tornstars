//*****************************************************************************/
//
// tsShipCreation.pog
//
// This module looks up faction and fleet specific ships.
// This module also provides utility functions for the creation of
// ship escort groups. From basic form to more complex.
// 
// Core code taken from jcEShipCreation.pog by james.
//
// June 2004, James Walker
// Dec 2006, Jon Freise, rewritten to use caching to avoid timeout failure.
//****************************************************************************/

package	tsShipCreation;


uses
	Debug,
	String,
	INIFile,
	List,
	Set,
	Global,
	iShip,
	iFaction,
	Math,
	Group,
   iUtilities,
   tsShipCreationEnum,
   tsFaction;


	
provides GetShip,
         ShipClassTemplate,
         ShipType2String,
         String2ShipType,
         Init,
         TestINIFile;

 



//
// Prototypes
//
prototype string ShipClassTemplate(string faction, string fleet_style, string ship_class);
prototype string iShipCreation.GetShip( hfaction faction, etsShipType ship_type );
prototype string ShipType2String(etsShipType ship_type);


prototype string choose_ship(hsim root, string subsection, string ship_class, int item);
prototype hsim create_subsection(hinifile hini, string subsection, bool must_have);



string GetShip( hfaction faction, etsShipType ship_type )
{
   string faction_name, fleet_style, ship_class;

   ship_class = ShipType2String(ship_type);

   faction_name = tsFaction.EName(faction);

   fleet_style = tsFaction.FleetStyle(faction);

   return ShipClassTemplate(faction_name, fleet_style, ship_class);
}


//
// Fn: ShipClassTemplate
// Desc: Returns a fully qulaified sim template string for use in tsShip.Create.
//       This function pulls this data from the faction_ships.ini mapping file.
//
string ShipClassTemplate(string faction, string fleet_style, string ship_class)
{
   string ship_template;
   hsim root;
   
   root = Sim.Cast(Global.Handle("ts_ship_creation_root"));

   ship_template = choose_ship(root, faction, ship_class, 99);

   if ("" == ship_template) {
      ship_template = choose_ship(root, fleet_style, ship_class, 99);

      if ("" == ship_template) {
         ship_template = choose_ship(root, "default", ship_class, 99);
      }
   }

	return ship_template;
}


// Allow asking for a specific ship. This allows automated testing, as well
// as random selection.
string choose_ship(hsim root, string subsection, string ship_class, int item)
{
   hsim holder;
   int index, entries;
   string key_name;

   if (Object.PropertyExists(root, subsection)) {
      holder = Sim.Cast(Object.HandleProperty(root, subsection));
      
      if (Object.PropertyExists(holder, ship_class)) {

         if (99 == item) {
            entries = Object.IntProperty(holder, ship_class);
            index = Math.RandomInt(0, (entries-1));
         }else {
            index = item;
         }
         
         key_name = String.Join(ship_class, String.FromInt(index));

         return Object.StringProperty(holder, key_name);
      }
   }

   return "";
}


string ShipType2String(etsShipType ship_type)
{
   switch (ship_type) {
	case EST_Gunstar: return "EST_Gunstar";break;

	case EST_Drone: return "EST_Drone";break;
	case EST_Waldo: return "EST_Waldo";break;
	case EST_CommandSection: return "EST_CommandSection";break;
	case EST_Utility: return "EST_Utility";break;
	case EST_Passenger: return "EST_Passenger";break;
	case EST_Fighter: return "EST_Fighter";break;
	case EST_Tug: return "EST_Tug";break;
	case EST_Patcom: return "EST_Patcom";break;
	case EST_Interceptor: return "EST_Interceptor";break;
	case EST_Corvette: return "EST_Corvette";break;
	case EST_Freighter: return "EST_Freighter";break;
	case EST_Destroyer: return "EST_Destroyer";break;
	case EST_Cruiser: return "EST_Cruiser";break;
	case EST_Carrier: return "EST_Carrier";break;  

	case EST_FreightLight: return "EST_FreightLight";break; 
   case EST_FreightHeavy: return "EST_FreightHeavy";break; 
   case EST_FreightAll: return "EST_FreightAll";break; 
   case EST_FreightSuperHeavy: return "EST_FreightSuperHeavy";break; 
	case EST_FreightCourier: return "EST_FreightCourier";break; 

   case EST_CombatFighter: return "EST_CombatFighter";break; 
   case EST_CombatLight: return "EST_CombatLight";break; 
   case EST_CombatHeavy: return "EST_CombatHeavy";break;
	case EST_CombatCaptial: return "EST_CombatCapital";break; 
   case EST_CombatAll: return "EST_CombatAll";break; 

   case EST_Medical: return "EST_Medical";break; 
   case EST_Interface: return "EST_Interface";break; 
   case EST_PassengerLight: return "EST_PassengerLight";break; 
	case EST_PassengerHeavy: return "EST_PassengerHeavy";break; 
   case EST_PassengerAll: return "EST_PassengerAll";break; 

   case EST_MiningLight: return "EST_MiningLight";break; 
   case EST_MiningHeavy: return "EST_MiningHeavy";break; 
   case EST_MiningAll: return "EST_MiningAll";break;

	case EST_TankerLight: return "EST_TankerLight";break; 
   case EST_TankerHeavy: return "EST_TankerHeavy";break; 
   case EST_TankerAll: return "EST_TankerAll";break; 

   case EST_GeneralPurpose: return "EST_GeneralPurpose";break; 
   case EST_Flitter: return "EST_Flitter";break; 
   case EST_Recovery: return "EST_Recovery";break;
	case EST_DemoShips: return "EST_DemoShips";break; 
   case EST_Taxi: return "EST_Taxi";break;
   default:
      debug {
         Debug.PrintString("tsShipCreation.ShipType2String Error invalid type\n");
         Debug.PrintString(String.FormatInt("%d invalid type\n",ship_type));
      }
      break;
   }

   return "";
}


etsShipType String2ShipType(string type_name)
{
	if ("EST_Gunstar"==type_name) { return EST_Gunstar; }
	else if ("EST_Drone"==type_name) { return EST_Drone; }
	else if ("EST_Waldo"==type_name) { return EST_Waldo; }
	else if ("EST_CommandSection"==type_name) { return EST_CommandSection; }
	else if ("EST_Utility"==type_name) { return EST_Utility; }
	else if ("EST_Passenger"==type_name) { return EST_Passenger; }
	else if ("EST_Fighter"==type_name) { return EST_Fighter; }
	else if ("EST_Tug"==type_name) { return EST_Tug; }
	else if ("EST_Patcom"==type_name) { return EST_Patcom; }
	else if ("EST_Interceptor"==type_name) { return EST_Interceptor; }
	else if ("EST_Corvette"==type_name) { return EST_Corvette; }
	else if ("EST_Freighter"==type_name) { return EST_Freighter; }
	else if ("EST_Destroyer"==type_name) { return EST_Destroyer; }
	else if ("EST_Cruiser"==type_name) { return EST_Cruiser; }
	else if ("EST_Carrier"==type_name) { return EST_Carrier; }  

	else if ("EST_FreightLight"==type_name) { return EST_FreightLight; } 
   else if ("EST_FreightHeavy"==type_name) { return EST_FreightHeavy; } 
   else if ("EST_FreightAll"==type_name) { return EST_FreightAll; } 
   else if ("EST_FreightSuperHeavy"==type_name) { return EST_FreightSuperHeavy; } 
	else if ("EST_FreightCourier"==type_name) { return EST_FreightCourier; } 

   else if ("EST_CombatFighter"==type_name) { return EST_CombatFighter; } 
   else if ("EST_CombatLight"==type_name) { return EST_CombatLight; } 
   else if ("EST_CombatHeavy"==type_name) { return EST_CombatHeavy; }
	else if ("EST_CombatCapital"==type_name) { return EST_CombatCaptial; } 
   else if ("EST_CombatAll"==type_name) { return EST_CombatAll; } 

   else if ("EST_Medical"==type_name) { return EST_Medical; } 
   else if ("EST_Interface"==type_name) { return EST_Interface; } 
   else if ("EST_PassengerLight"==type_name) { return EST_PassengerLight; } 
	else if ("EST_PassengerHeavy"==type_name) { return EST_PassengerHeavy; } 
   else if ("EST_PassengerAll"==type_name) { return EST_PassengerAll; } 

   else if ("EST_MiningLight"==type_name) { return EST_MiningLight; } 
   else if ("EST_MiningHeavy"==type_name) { return EST_MiningHeavy; } 
   else if ("EST_MiningAll"==type_name) { return EST_MiningAll; }

	else if ("EST_TankerLight"==type_name) { return EST_TankerLight; } 
   else if ("EST_TankerHeavy"==type_name) { return EST_TankerHeavy; } 
   else if ("EST_TankerAll"==type_name) { return EST_TankerAll; } 

   else if ("EST_GeneralPurpose"==type_name) { return EST_GeneralPurpose; } 
   else if ("EST_Flitter"==type_name) { return EST_Flitter; } 
   else if ("EST_Recovery"==type_name) { return EST_Recovery; }
	else if ("EST_DemoShips"==type_name) { return EST_DemoShips; } 
   else if ("EST_Taxi"==type_name) { return EST_Taxi; }

   debug {
       Debug.PrintString("tsShipCreation.String2ShipType Error invalid type :");
       Debug.PrintString(type_name);
       Debug.PrintString("\n");
    }

   return EST_Invalid;
}



test_subsection(hinifile hini, string subsection, bool must_have)
{
   int entries, j, i;
   string ship_class;
   string result, stored;
   hship ship;
   hsim root;

   root = Sim.Cast(Global.Handle("ts_ship_creation_root"));
   // Loop through all ship choices and 
   // create at least one from each.
   for (i=EST_Gunstar;i<EST_Max;++i) {
      ship_class = ShipType2String(i);

      if (INIFile.NumberedExists( hini, subsection, ship_class, 0 )) {
         entries = iUtilities.FindNumberOfINIEntries( hini, subsection, ship_class );

         // Loop through all the entries for each type of ship.
         for (j=0;j<entries;++j) {

            result = INIFile.NumberedString( hini, subsection, ship_class, j, "" );
            stored = choose_ship(root, subsection, ship_class, j);

            if (result != stored) {
               debug {
                  Debug.PrintString("tsShipCreation.TestINIFile cach mismatch Error\n");
                  Debug.PrintString(subsection);
                  Debug.PrintString(" Subsection\n");
                  Debug.PrintString(ship_class);
                  Debug.PrintString(" ship class\n");
                  Debug.PrintString(result);
                  Debug.PrintString(" template\n");                  
               }
            }

            ship = iShip.Create(result,"testship");

            if (none == ship) {
               // This is not a valid entry. Print it.
               debug {
                  Debug.PrintString("tsShipCreation.TestINIFile Error\n");
                  Debug.PrintString(subsection);
                  Debug.PrintString(" Subsection\n");
                  Debug.PrintString(ship_class);
                  Debug.PrintString(" ship class\n");
               }
            }else {
               // Clean up.
               Sim.Destroy(ship);
            }

         }// for each entry
      }else {
         if (must_have) {
            debug {
               Debug.PrintString("tsShipCreation.TestINIFile Error missing entry\n");
               Debug.PrintString(subsection);
               Debug.PrintString(" Subsection\n");
               Debug.PrintString(ship_class);
               Debug.PrintString(" ship class\n");

            }
         }
      }
   }// for each ship class  
}

TestINIFile()
{
   set factions;
   hfaction faction;
   int total, i, j, entries, count;
   string template_name;
   string subsection;
   string ship_class;
   hship ship;
   hinifile hini = INIFile.Create("ini:/faction_ships");

   debug Debug.PrintString("tsShipCreation.TestINIFile starts\n");

   
   if (none == hini) {
      debug Debug.PrintString("tsShipCreation.TestINIFile error no file\n");
      return;
   }


   factions = Set.FromList(iFaction.All());
   while (!Set.IsEmpty(factions)) {
      faction = iFaction.Cast(Set.FirstElement(factions));
      Set.Remove(factions, faction);

      test_subsection(hini, tsFaction.EName(faction), false);
   }


   // Test the default
   test_subsection(hini, "default", true);

   // Test each fleet style
   test_subsection(hini, "leung", false);

   test_subsection(hini, "sultan", false);

   test_subsection(hini, "barrens", false);

   test_subsection(hini, "cmc", false);

   test_subsection(hini, "machine", false);

   test_subsection(hini, "brethren", false);

   count = 0;
   while (INIFile.NumberedExists(hini, "Subsections","name",count)) {
      subsection = INIFile.NumberedString(hini, "Subsections","name",count,"");

      test_subsection(hini, subsection, false);

      ++count;
   }

   INIFile.Destroy(hini);

   debug Debug.PrintString("tsShipCreation.TestINIFile ends\n");
}


// Call init after the factions are initilized, but before anything else needs
// ships. System Enter.
Init()
{
   int count;
   string subsection;
   hinifile hini = INIFile.Create("ini:/faction_ships");
   hsim root, holder;
   bool must_have;

   // Build the list of ships, if this handle 
   root = Sim.Cast(Global.Handle("ts_ship_creation_root"));
   if ((none != root) && (!Sim.IsDead(root))) {
      return;
   }

   root = Sim.Create("ini:/sims/nav/waypoint","ship_creation_root");

   count = 0;
   while (INIFile.NumberedExists(hini, "Subsections","name",count)) {
      subsection = INIFile.NumberedString(hini, "Subsections","name",count,"");
      
      if ("default" == subsection) {
         must_have = true;
      }else {
         must_have = false;
      }

      holder = create_subsection(hini, subsection, must_have);

      Object.AddHandleProperty(root, subsection, holder);

      ++count;
   }

   Global.CreateHandle("ts_ship_creation_root", GA_Write|GA_NoSave, root);
}



hsim create_subsection(hinifile hini, string subsection, bool must_have)
{
   int entries, j, i;
   string ship_class;
   string result;
   string key_name;
   hship ship;
   hsim holder;

   holder = Sim.Create("ini:/sims/nav/waypoint",subsection);

   // Loop through all ship choices and 
   // create at least one from each.
   for (i=EST_Gunstar;i<EST_Max;++i) {
      ship_class = ShipType2String(i);

      if (INIFile.NumberedExists( hini, subsection, ship_class, 0 )) {
         entries = iUtilities.FindNumberOfINIEntries( hini, subsection, ship_class );

         // Record the number of entries.
         Object.AddIntProperty(holder, ship_class, entries);

         // Loop through all the entries for each type of ship.
         for (j=0;j<entries;++j) {
            result = INIFile.NumberedString( hini, subsection, ship_class, j, "" );
            if ("" != result) {
               key_name = String.Join(ship_class,String.FromInt(j));
               Object.AddStringProperty(holder, key_name, result);
            }
         }// for each entry
      }else {
         if (must_have) {
            debug {
               Debug.PrintString("tsShipCreation.create_subsection Error missing entry\n");
               Debug.PrintString(subsection);
               Debug.PrintString(" Subsection\n");
               Debug.PrintString(ship_class);
               Debug.PrintString(" ship class\n");
            }
         }
      }
   }// for each ship class  


   return holder;
}
