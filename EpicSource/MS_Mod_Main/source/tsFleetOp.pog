/*******************************************************************************************/
//
// tsFleetOp
//
// These objects represent fleet operations in game. They are persistant fleets that 
// live through saves. They track surviving ships. They track fleets current location.
//	
// 
//
// October, 2003
// Reworked May 2005 jon freise, adding into Middle States release.
// Reworked March 2006 jf, changed to be fleet operations only.
//******************************************************************************************/

package tsFleetOp;

// Imports
uses  Sim,
	iSim,
	Debug,
	Global,
	iGame,
	iFaction,
	INIFile,
	Input,
	List,
	Object,
	String,
//	Task,
	State,
	Text,
	Math,
	iShip,
	iHabitat,
	iMapEntity,
   iCargo,
   CargoTypes,
   
	tsArray,
	
	tsFleetOpEnum,
	tsFaction,
	tsStationEnum,
	tsStation,
   tsEconUtil,
	
   tsPogClass,
   tsPcOwner,
   
	tsLRoot,
	tsLProduction,
	tsLCenter,
	tsFleetOpPhysical,
	tsLFleet;


// Exports
provides 
	NewOp,
	Create,
	DestroyOp,
	IsOperation,
   GetOperationType,
   GetOperationPog,
   GetOperationEnvFoTemplate,
   GetOperationMaxFleets,
   GetOperationShipClass,
	Find,
	SetKnown,
	IsKnown,
	EventTimer,
	SetEventTimer,
	WakeTimer,
	SetWakeTimer,
	IsCreated,
	SetCreated,
	Name,
	SetName,
	ShipTemplate,
	SetShipTemplate,
	OperationList,
	StationOperations,
	StationOperationsOfType,
	SupportedOperations,
	SetSupporter,
	Supporter,
	DisplayOps,
	KillShipOnOp,
	SetShipOnOp;





//*******************************************************************************************/         
//
//*******************************************************************************************/



//*******************************************************************************************/
// 
//*******************************************************************************************/

prototype bool IsKnown(hisim operation);

prototype list OperationList();

prototype hisim Create(int op_id, hfaction faction, bool known, int wake_day);

prototype DisplayOps();

prototype bool IsOperation(hisim operation);

prototype string Name(hisim operation);

prototype add_list(hisim operation);

prototype hhabitat Supporter(hisim operation);

prototype string GetOperationEnvFoTemplate(hisim operation);

/* 
Operation Notes:

Operations have an running cost. You must pay the cost even if no production. The operation can go 'underground', to 
avoid snooping police and agents, but then it cannot generate profits or complete missions. 

All operations have a list of station types where they might be created. Some need resource attributes to be 
present.

Operations are state objects. They have these properties.

* Station Name
* Hide Value - how hidden is the operation. Determined by type.
* Operation Type
* Owning Faction
* Parent Boss (might be none)
* Boss Name
* Relation State
    Independent (top level)
    Joined (has parent)
* Relation Level - hate if forced to join, love if started by faction.
* Boss Decision Level - determines the kind of missions started here.
    Trade and Operation only
    Create new Suboperations
    Alliance and Diplomacy 
* Security Level - how expensive protections, hard to get informers inside.
* Alarm Value - how much public alarm does this operation raise.


Stations are allowed only one outlaw operation. Each station has an 
operation handle list attached, to quickly find operations.
*/



list SupportedOperations(hhabitat station)
{
   return Object.ListProperty(station,"gtsFleetOpSupported");
}

// Removes this operation from its current supporter.
RemoveSupporter(hisim operation)
{
   list attach_list;
   hhabitat station = Supporter(operation);

   if (station != none) {
      atomic {
         if (Object.PropertyExists(station,"gtsFleetOpSupported")) {
            attach_list = Object.ListProperty(station,"gtsFleetOpSupported");
            List.Remove(attach_list, operation);
            Object.SetListProperty(station,"gtsFleetOpSupported", attach_list);
         }
      }
   }

   Object.AddHandleProperty(operation,"gtOpHome", none);
}

SetSupporter(hisim operation, hhabitat station)
{
   list attach_list;

   if (station == none) {
      return;
   }

   atomic {
      if (!Object.PropertyExists(station,"gtsFleetOpSupported")) {
         List.AddTail(attach_list, operation);
         Object.AddListProperty(station,"gtsFleetOpSupported", attach_list);
      }else {
         attach_list = Object.ListProperty(station,"gtsFleetOpSupported");
         List.AddTail(attach_list, operation);
         Object.SetListProperty(station,"gtsFleetOpSupported", attach_list);
      }
   }

   // Attach this station to the operation for fast return link.
   Object.AddHandleProperty(operation,"gtOpHome", station);
}



hhabitat Supporter(hisim operation)
{
   return iHabitat.Cast(Object.HandleProperty(operation,"gtOpHome"));
}




hisim Find(int operation_id)
{
   list op_list = OperationList();
   int i, total;
   hisim op;

   total = List.ItemCount(op_list);
   for (i=0;i<total;++i) {
      op = iSim.Cast(List.GetNth(op_list, i));

      if (operation_id == tsLRoot.GetId(op)) {
         return op;
      }
   }

   return none;
}



DestroyOp(hisim operation)
{
   hisim op;
   list op_list;
   hhabitat station;

   debug {
      Debug.PrintString("UnderOp.DestroyOp ");
      Debug.PrintString(Text.Field(Name(operation), FT_Text));
      Debug.PrintString("\n");
   }

   if (none != operation) {
      atomic {
         op_list = Global.List("gt_underop_all_list");
         List.Remove(op_list, operation);
         Global.SetList("gt_underop_all_list", op_list);
      }

      station = Supporter(operation);

      // Clear the operation from the supporters list.
      RemoveSupporter(operation);

      // Let the station know that it lost an operation.
      tsStation.SetAwake(station, true);

      // Clear the operation from the centers list.
      tsLCenter.SetCenter(operation, none);

      // Remove the operation sim.
      Sim.Destroy(operation);
   }
}



list OperationList()
{
   return Global.List("gt_underop_all_list");

}



list StationOperations(hisim station)
{
   list op_list = tsLCenter.GetAttached(station);
   list final_list;
   int i, total;
   hisim op;

   total = List.ItemCount(op_list);
   for (i=0;i<total;++i) {
      op = iSim.Cast(List.GetNth(op_list, i));
      
      if (IsOperation(op)) {
         List.AddTail(final_list, op);
      }
   }

   return final_list;
}



list StationOperationsOfType(hisim station, int type)
{
   list op_list = tsLCenter.GetAttached(station);
   list final_list;
   int i, total;
   hisim op;

   total = List.ItemCount(op_list);
   for (i=0;i<total;++i) {
      op = iSim.Cast(List.GetNth(op_list, i));
      
      if (IsOperation(op)) {
         if (type == tsLProduction.GetType(op)) {
            List.AddTail(final_list, op);
         }
      }
   }

   return final_list;
}


SetEventTimer(hisim operation, int day)
{
   Object.AddIntProperty(operation,"gtsFleetOpEvent", day);
}

int EventTimer(hisim operation)
{
   return Object.IntProperty(operation,"gtsFleetOpEvent");
}


SetWakeTimer(hisim operation, int day)
{
   Object.AddIntProperty(operation,"gtsFleetOpWake", day);
}

int WakeTimer(hisim operation)
{
   return Object.IntProperty(operation,"gtsFleetOpWake");
}


SetKnown(hisim operation, bool known)
{
   Object.AddBoolProperty(operation,"gtsFleetOpKnown", known);
}

bool IsKnown(hisim operation)
{
   return Object.BoolProperty(operation,"gtsFleetOpKnown");
}


// This function is used to track if an operation already has an in game
// representation. This function should be set true when 
// an in game version is created, and set false when that version
// has been culled.
// This is not a saved property (has no meaning after a restore).
SetCreated(hisim operation, bool existing)
{
   Object.AddBoolProperty(operation,"gtsFleetOpExists", existing);
}


bool IsCreated(hisim operation)
{
   return Object.BoolProperty(operation,"gtsFleetOpExists");
}


// The name is remembered in the save file. This allows
// the operation core ship to have the same name
// each time the player encounters it.
SetName(hisim operation, string name)
{
   Object.AddStringProperty(operation,"gtsFleetOpName", name);
}

string Name(hisim operation)
{
   return Object.StringProperty(operation,"gtsFleetOpName");
}

// The core ship template is remembered. This allows
// the core ship to be the same every time the player
// encounters this operation.
SetShipTemplate(hisim operation, string ship_template)
{
   Object.AddStringProperty(operation,"gtsFleetOpTemplate", ship_template);
}

string ShipTemplate(hisim operation)
{
   return Object.StringProperty(operation,"gtsFleetOpTemplate");
}

bool IsOperation(hisim operation)
{
   if (none == operation) {
      return false;
   }
   if (Sim.IsDead(operation)) {
      return false;
   }

   if (Object.PropertyExists(operation, "gtsFleetOpKnown")) {
      return true;
   }

   return false;
}


// Return the operation type
int GetOperationType(hisim operation)
{
   return tsLProduction.GetType(operation);
}

// Return the environment template name associated with the operation
string GetOperationEnvFoTemplate(hisim operation)
{
   return Object.StringProperty(operation, "gtOpEnvFoTemplate");
}

// Return the operation pog as a string
string GetOperationPog(hisim operation)
{
   return Object.StringProperty(operation, "gtOpPog");
}

// Return the primary ship class for the operations lead ship
string GetOperationShipClass(hisim operation)
{
   return Object.StringProperty(operation, "gtOpShipClass");
}

// Return the max fleet value for the operation as defined
// for the operation in the metagame_production.ini
int GetOperationMaxFleets(hisim operation)
{
   return Object.IntProperty(operation, "gtOpMaxFleets");
}


/* Create a new operation of the specified type, of a faction at a map location.

Desc:
- The operation defn file is loaded and the required operation sim created.
- A set of specific details are loaded and set against the operation Sim as follows
	* environment FO template
	* operation pog
	* main operation ship class - this is the primary ship that represents this operation
	* escort ship class - the class used for any escorts created
	* max fleet value
*/
hisim NewOp(int op_type,
            hfaction faction,
            hmapentity center)
{
   int id = tsLRoot.NewId();
   hisim operation;
   hinifile operation_defn_file;
	hinifile operation_env_file;
	hinifile operation_pc_file;
   string env_fo_template;
   string operation_pog;
   string main_ship_class;
   int max_fleets;


   operation_defn_file = INIFile.Create("ini:/metagame_production");
   if( none == operation_defn_file) {
      debug Debug.PrintString("tsFleetOp.NewOp: ERROR: unable to open metagame_production.ini\n");
      return none;
   }


   operation = Create(id, faction, false, 1);

   // get specific details for the operation to be used consistently later
   env_fo_template = INIFile.NumberedString( operation_defn_file, tsEconUtil.StationIntToString(op_type), "ESS_Normal", 0, "ERROR");
   operation_pog = INIFile.NumberedString( operation_defn_file, tsEconUtil.StationIntToString(op_type), "operation_pog", 0, "none");
   main_ship_class = INIFile.String( operation_defn_file, tsEconUtil.StationIntToString(op_type), "ship_class", "none");
   max_fleets = INIFile.Int(operation_defn_file, tsEconUtil.StationIntToString(op_type), "max_fleets",0);
	
	
	// set the values on the object
   Object.AddStringProperty(operation, "gtOpEnvFoTemplate", env_fo_template);
   Object.AddStringProperty(operation, "gtOpPog", operation_pog);
   Object.AddStringProperty(operation, "gtOpShipClass", main_ship_class);
   Object.AddIntProperty(operation, "gtOpMaxFleets", max_fleets);

	// finish with the ini file.
   INIFile.Destroy(operation_defn_file);


   // sets up the production center and sets some key values against it e.g. operation type
   // tsLProduction.Create(hisim producer, int type, int funds, int loss, int loans, bool active)
   tsLProduction.Create(operation, op_type, 100, 0, 0, true);

   // tsLFleet.Create(hisim producer, int fleets, int supplies, int max_fleets, int max_supplies)
   tsLFleet.Create(operation, max_fleets, 200, max_fleets, 200);

   // New operations are created at thier owner station.
   SetSupporter(operation, iHabitat.Cast(center));

   // New operations are placed at thier owner station.
   tsLCenter.Create(operation, center);

   return operation;
}


// Create the actual operation sim
hisim Create(int op_id,
             hfaction faction,
             bool known,
             int wake_day)

{

   list station_list, empty_list;
   hisim operation;
   hisim efaction;
   list op_list;
   string name, station_name;

   name = "op placeholder";

   operation = iSim.Cast(Sim.Create("ini:/sims/nav/waypoint", name));


   tsLRoot.SetId(operation, op_id);

   SetKnown(operation, known);
   SetEventTimer(operation, 0);
   SetWakeTimer(operation, wake_day);
   SetCreated(operation, false);

   add_list(operation);

   iSim.SetFaction(operation, faction);

   return operation;
}


add_list(hisim operation)
{
   list op_list;

   atomic {
      if (Global.Exists("gt_underop_all_list")) {
         op_list = Global.List("gt_underop_all_list");
         List.AddTail(op_list, operation);
         Global.SetList("gt_underop_all_list",op_list);
      }else{
         List.AddTail(op_list, operation);
         Global.CreateList("gt_underop_all_list",GA_Write, op_list);
      }
   }
}





string Operation2String(int op_type)
{

   switch (op_type) {
   case ept_PirateHQOp:
      return "Pirate Command Center";
      break;
   // Planted on production stations to drain off funds for use by pirates.
   case ept_EmbezzleOp:
      return "Embezzling Operation";
      break;
   // Designed to support the player so they can refuel their ships
   case ept_TankerOp:
      return "Tanker Operation";
      break;
   // Planted on entertainment stations and habitats to generate funds for pirates.
   case ept_ViceDenOp:
      return "Vice Den Operation";
      break;
   // Planted on research stations to create and sell hacking tools.
   case ept_HackerOp:
      return "Hacker Tools Operation";
      break;
   // Planted on a medical station to provide and sell Cyber gear, new identities, transplants.
   case ept_CyberOp:
      return "Cyber Tools Operation";
      break;
   //Planted by pirates on anyone elses station. This provides defense fleets for the
   //station, but at a cost to the station. 
   case ept_ProtectionOp:
      return "Protection Operation";
      break;
   // A special operation, used to attack and destroy a specific target faction
   // from a specific location. Replaces the "plan" system.
   case ept_FleetAttackOp:
      return "Attack Planning";
      break;
   // A special operation that adds extra defense to a station.
   case ept_FleetDefenseOp:
      return "Defense Planning";
      break;
   // A covert operations center, with the goal of destroying a faction.
   case ept_SpyAttackOp:
      return "Covert Attack Planning";   
      break;
   case ept_SpyDefenseOp:
      return "Covert Defense Planning";   
      break;
   // A covert operations center, with the goal of neutralizing a 
   // faction to break them away from allies.
   case ept_SpyNeutralOp:
      return "Covert Neutralize Planning";   
      break;
   // A special operation that signifies a major invasion of another
   // system. Run by an admiral and launched only from a Defense Dock,
   // the invasion tries to capture opposing stations, and destroy
   // rival fleets.
   case ept_InvasionOp:
      return "Invasion Planning";   
      break;

   // This base draws funds and people from sympathetic habitats. 
   case ept_RebelSupportOp:
      return "Rebel Recruitment";
      break;

   // Designed to support the player so they can buy weapons for their ships
   case ept_ArmsDealerOp:
      return "Arms Dealer Operation";
      break;

   // Designed to support the player so they can hire wingmen
   case ept_MercOp:
      return "Mercenary Operation";
      break;
		
	case ept_FreeTraderOp:
		return "Free Trader Operation";
		break;

	case ept_FreeMinerOp:
		return "Free Miner Operation";
		break;

	case ept_FreeTankerOp:
		return "Free Tanker Operation";
		break;

	case ept_FreeScienceOp:
		return "Free Science Operation";
		break;

	case ept_FreeMerchantOp:
		return "Free Merchant Operation";
		break;

   // Creates a black market importer/exporter into the station.
   // Hides just outside of station range, and deals in goods with
   // the criminal element at marked up prices.
   // Especially popular when attached to weapons stations.
   case ept_BlackMarketOp:
      return "Black Market";
      break;
   case ept_RebelHQOp:
      return "Rebel Command Center";
      break;
   default:
      break;
   }

   return "Invalid Operation";
}



DisplayOps()
{
   int op_count, link_count, opnum, linknum;
   list op_list, link_list;

   hisim operation;

   int iproperty;
   string sproperty;

   debug Debug.PrintString("tsFleetOp.DisplayOps\n");
   // Get list of operations
   // Get count of operations
   op_list = OperationList();
   op_count = List.ItemCount(op_list);

   for (opnum=0;opnum<op_count;++opnum) {
      operation = iSim.Cast(List.GetNth(op_list,opnum));
      debug{
         Debug.PrintString(String.FormatInt("Op %d ",tsLRoot.GetId(operation)));
         Debug.PrintHandle(operation);
         Debug.PrintString("\n");
      }
   }
}



////////////////////////////////////////////////////////////////////////////////////////////
//
// 'Ship on Pog' Object management
//
///////////////////////////////////////////////////////////////////////////////////////////


//
// Set a ship on object to 'killed'
// Finds based on handle. Handle of 'none' is not valid.
//
KillShipOnOp(hisim operation, hship ship)
{
   int index, size;
   
	// make sure is a valid handle
	if(operation == none || ship == none)
	{
		debug Debug.PrintString("tsFleetOp.KillShipOnOp: operation or ship was none\n");
		return;
	}
	
   // find the ship in the operation ship array using handle as a key,
   // when found set handle to none, mark as dead
   size = tsArray.ArraySize(operation, "ts_ShipHandle");
   
   for(index=0; index < size; ++index)
   {
      if( tsArray.GetArrayHandle(operation, "ts_ShipHandle", index) == ship )
      {
         debug Debug.PrintString("tsFleetOp.KillShipOnOp: Ship killed off from op\n");
         tsArray.SetArrayHandle(operation, "ts_ShipHandle", index, none);
         tsArray.SetArrayBool(operation, "ts_ShipAlive", index, false);
         break;
      }
   }
}

//
// Add/set a ship detail to the operation
//
SetShipOnOp(hisim operation, hship ship, int index)
{
	string ship_template;
	int threat;
	
	// make sure is a valid handle
	if(operation == none || ship == none)
	{
		debug Debug.PrintString("tsFleetOp.SetShipOnOp: operation or ship was none\n");
		return;
	}
	
	ship_template = Sim.TemplateName( Sim.Cast(ship) );
	threat =  Object.FloatProperty(ship, "threat");
   
	tsArray.SetArrayString(operation, "ts_ShipTemplates", index, ship_template);
	tsArray.SetArrayBool(operation, "ts_ShipAlive", index, true);
	tsArray.SetArrayHandle(operation, "ts_ShipHandle", index, ship);
	tsArray.SetArrayInt(operation, "ts_ShipThreat", index, threat);
}

//
// Find the next ship in a Live or Dead state starting from an index
//
int FindShipOnOp(hsim operation, bool state, int start_index)
{
	int index, size, found;

	// make sure is a valid handle
	if(operation == none)
	{
		debug Debug.PrintString("tsFleetOp.FindShipOnOp: operation was none\n");
		return -1;
	}
	
	found = -1;
	size = tsArray.ArraySize(operation, "ts_ShipHandle");
	for(index=start_index; index < size; ++index)
	{
		if(tsArray.GetArrayBool(operation, "ts_ShipAlive", index) == state)
		{
			found = index;
			break;
		}
	}

	return found;
}


/*
   Finding Operations:

   Recruiting Operations:
   
   Some operations require finding a willing accomplice high 
   up inside the station management. These operations
   require extensive recruiting steps as the first part of setup.
*/
