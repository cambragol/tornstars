/*******************************************************************************************/
//
// tsFoCustoms.pog
// 
// Torn Stars Fleet Operation Customs
// 
// The customs fleet operation collects taxes from player ships in the star system.
// 
// 
// March 2006
//******************************************************************************************/

package tsFoCustoms;


uses  Sim,
iSim,
Debug,
Global,
iGame,
iFaction,
INIFile,
Input,
List,
Object,
String,
Task,
State,
Text,
Math,
iShip,
iHabitat,
iMapEntity,
   iLagrangePoint,
   iHUD,
   iComms,
   tStationEnum,
   tStation,
   tEFaction,
   tTime,
   tUnderOp,
   tLCenter,
   tLProduction,
   tLFleet,
   tStationFilter,
   tShipName,
   tEShipCreation,
   tsFleetOpCombat,
   tsDebug;




provides New,
         Save,
         Restore,
         Update;



prototype bool is_production(hmapentity center);

prototype hhabitat find_production_station(hisim operation, hmapentity center, string system_name);

prototype hlagrangepoint find_lpoint(hisim operation, hmapentity center, string system_name);

prototype hhabitat find_non_dislike_station(hisim operation, hmapentity center, string system_name);

//*******************************************************************************************/         
//
//*******************************************************************************************/




New(hisim operation)
{
   string ship_name, ship_template;
   hfaction faction;

   faction = iSim.Faction(operation);

   // We pick an underworld name for ourselves. 
   // using a large number means we get a random name.
   ship_name = tShipName.ShipName("police", 10000);

   // Also pick a large freighter for collecting player sales.
   ship_template = tEShipCreation.GetShip(faction, EST_CombatLight);

   tUnderOp.SetName(operation, ship_name);
   tUnderOp.SetShipTemplate(operation, ship_template);

   tLFleet.SetMaxFleets(operation, 200);
   tLFleet.SetFleets(operation, 200);
}



Save(hisim operation)
{
   // Nothing to save for this operation type.


}



Restore(hisim operation)
{
   // Nothing to restore for this operation type.


}



Update(hisim operation)
{
   hmapentity center = tLCenter.GetCenter(operation);
   hhabitat target_station, supporter_station;
   bool hostiles, wounded, dying, out_gunned, home;
   bool move_home, move_safe, stay_still;
   list local_operations;
   hisim visitor;
   hfaction faction, visitor_faction;
   int i, total;
   int total_hostiles, hostile_fleets, fleets;
   int update_delay = 1;
   int current_day = tTime.GetDay();
   int patrol_day;
   hlagrangepoint lpoint;


  // debug tsDebug.PrintString("tsfocustoms_debug","tsFoCustoms.Update\n");

   
   faction = iSim.Faction(operation);


   local_operations = tUnderOp.StationOperations(center);
   List.Remove(local_operations, operation);
   total = List.ItemCount(local_operations);

   hostiles = false;
   total_hostiles = 0;

   for (i=0;i<total;++i) {
      visitor = iSim.Cast(List.GetNth(local_operations,i));
      visitor_faction = iSim.Faction(visitor);

      if (iFaction.Feeling(visitor_faction, faction) < -0.2) {
         // There are hostile forces at this station
         fleets = tLFleet.Fleets(operation);
         hostile_fleets = tLFleet.Fleets(operation);

         if (fleets > hostile_fleets) {
            // We have the advantage, pound them.
            tsFleetOpCombat.Offense(center, operation, visitor);
         }else {
            // We are outnumbered, so switch to evasive tactics.
            tsFleetOpCombat.Defense(center, operation, visitor);
         }
         // Remember this total, so we can choose to flee later.
         total_hostiles += hostile_fleets;
         hostiles = true;
      }
   }


   fleets = tLFleet.Fleets(operation);

   if (fleets == 0) {
      // Ouch, we died.
      tsDebug.PrintString("tsfocustoms_debug",
                          String.FormatStrStr("tsFoCustoms %s died\n%s",
                                              Text.Field(tUnderOp.Name(operation),FT_Text),
                                              ""));
      tUnderOp.DestroyOp(operation);
      return;
   }

   // Are we damaged
   if (fleets < tLFleet.MaxFleets(operation)) {
      wounded = true;
   }else {
      wounded = false;
   }

   // Are we dying? Anything over half damage for protection operations.
   if (fleets < (tLFleet.MaxFleets(operation)/2)) {
      dying = true;
   }else {
      dying = false;
   }

   // Check if we are outnumbered.
   if (fleets < total_hostiles) {
      out_gunned = true;
   }else {
      out_gunned = false;
   }


   supporter_station = tUnderOp.Supporter(operation);

   // Are we home?
   if (center == iMapEntity.Cast(supporter_station)) {
      home = true;
   }else {
      home = false;
   }


   // Repair some fleets. Double rate if at home.
   if (home) {
      tLFleet.ChangeFleets(operation, 20 * update_delay);
   }else {
      tLFleet.ChangeFleets(operation, 10 * update_delay);
   }


   move_home = false;
   move_safe = false;
   stay_still = false;

   if (dying && !home) {
      // Badly hurt, flee home.
      move_home = true;
   }else if (wounded && home) {
      // Stay here to heal.
      stay_still = true;
   }else if (out_gunned) {
      // Retreat to a safe place.
      move_safe = true;
   }


   // Otherwise, pick a place to earn funds.
   if (move_home) {

      tLCenter.SetCenter(operation, supporter_station);

      debug {
         tsDebug.PrintString("tsfocustoms_debug",
                          String.FormatStrStr("tsFoCustoms %s moving home\n%s",
                                              Text.Field(tUnderOp.Name(operation),FT_Text),
                                              ""));
         tsDebug.PrintHandle("tsfocustoms_debug",supporter_station);
         tsDebug.PrintString("tsfocustoms_debug","\n");
      }

   }else if (move_safe) {

      target_station = find_non_dislike_station(operation, center, iSim.WorldName(center));

      if (none != target_station) {
         // To move an operation, we just move the center.
         tLCenter.SetCenter(operation, target_station);


         debug {
            tsDebug.PrintString("tsfocustoms_debug",
                                String.FormatStrStr("tsFoCustoms %s moving to safety\n%s",
                                              Text.Field(tUnderOp.Name(operation),FT_Text),
                                              ""));
            tsDebug.PrintHandle("tsfocustoms_debug",supporter_station);
            tsDebug.PrintString("tsfocustoms_debug","\n");
         }
      }

   }else if (!stay_still){
      
      patrol_day = tUnderOp.EventTimer(operation);
      if (patrol_day == 0) {
         patrol_day = Math.RandomInt(1,7);
         tUnderOp.SetEventTimer(operation, patrol_day);
      }

      if (current_day > patrol_day) {

         tUnderOp.SetEventTimer(operation, current_day + 7);

         lpoint = find_lpoint(operation, center, iSim.WorldName(center));

         if (none != lpoint) {
            // If there is an empty lpoint, take it.
            tLCenter.SetCenter(operation, lpoint);

            debug {
               tsDebug.PrintString("tsfocustoms_debug",
                                   String.FormatStrStr("tsFoCustoms %s patrol lpoint\n%s",
                                                       Text.Field(tUnderOp.Name(operation),FT_Text),
                                                       ""));
               tsDebug.PrintHandle("tsfocustoms_debug",lpoint);
               tsDebug.PrintString("tsfocustoms_debug","\n");
            }
         }else {
            // Look for a station.
            target_station = find_production_station(operation, center, iSim.WorldName(center));

            if (none != target_station) {
               // To move an operation, we just move the center.
               tLCenter.SetCenter(operation, target_station);

               debug {
                  tsDebug.PrintString("tsfocustoms_debug",
                                      String.FormatStrStr("tsFoCustoms %s patrol station\n%s",
                                                          Text.Field(tUnderOp.Name(operation),FT_Text),
                                                          ""));
                  tsDebug.PrintHandle("tsfocustoms_debug",target_station);
                  tsDebug.PrintString("tsfocustoms_debug","\n");
               }
            }
            // Nothing presents itself, defer move to next cycle.
         }
      }
   }

   // Next update, tomorrow.
   tUnderOp.SetWakeTimer(operation, tTime.GetDay() + update_delay);
}


// Return true is this location is an operating production station.
bool is_production(hmapentity center)
{
   if (T_Station == iSim.Type(center)) {
      if (ESS_Normal == tStation.Status(iHabitat.Cast(center))) {
         if ( (ESC_Mine|ESC_Process|ESC_Manufacture) & tStation.Category(iHabitat.Cast(center))){
            return true;
         }
      }
   }
   return false;
}

hhabitat find_production_station(hisim operation, hmapentity center, string system_name)
{
   set habitats;

   // Search system stations and find a production station where we can live.
   habitats = tStationFilter.OnStatusAndCategory(iMapEntity.SystemHabitatsInSystem(system_name),
                                                 ESS_Normal,
                                                 (ESC_Mine|ESC_Process|ESC_Manufacture));

   Set.Remove(habitats, center);
   // We can hang out at neutral or better stations. We avoid people trying to
   // kill us.
   habitats = tStationFilter.OnGreaterFeeling(habitats,
                                              iSim.Faction(operation),
                                              tEFaction.Type2LowerLevel(FT_Neutral));

   return iHabitat.Random(habitats);
}

// Move to a place to heal. Even empty stations are fine.
hhabitat find_non_dislike_station(hisim operation, hmapentity center, string system_name)
{
   set habitats;

   habitats = tStationFilter.OnGreaterFeeling(iMapEntity.SystemHabitatsInSystem(system_name),
                                              iSim.Faction(operation),
                                              tEFaction.Type2LowerLevel(FT_Neutral));
   Set.Remove(habitats, center);

   return iHabitat.Random(habitats);
}

// Return an Lpoint that is on a standard route and has no other customs 
// operations on it.  hlagrangepoint find_lpoint(hisim operation, 
hlagrangepoint find_lpoint(hisim operation, hmapentity center, string system_name)
{ 
set lpoints, final_set; 
hlagrangepoint lpoint; 
list lpoint_ops; 
hisim lpoint_op; 
int i, total; 
bool taken; 

   lpoints = iMapEntity.SystemLagrangePointsInSystem(system_name);

   Set.Remove(lpoints, center);

   while (!Set.IsEmpty(lpoints)) {
      lpoint = iLagrangePoint.Cast(Set.FirstElement(lpoints));
      Set.Remove(lpoints, lpoint);

      if (Object.BoolProperty(lpoint,"standard_route")) {
         lpoint_ops = tUnderOp.StationOperations(iSim.Cast(lpoint));

         taken = false;
         total = List.ItemCount(lpoint_ops);
         for (i=0;i<total;++i) {
            lpoint_op = iSim.Cast(List.GetNth(lpoint_ops, i));

            if (ept_CustomsOp == tLProduction.GetType(lpoint_op)){
               // Customs is already here
               taken = true;
            }
         }

         // If the lpoint is not taken, we grab it.
         if (!taken) {
            Set.Add(final_set, lpoint);
         }
      }
   }

   if (Set.IsEmpty(final_set)) {
      return none;
   }

   return iLagrangePoint.Cast(Set.FirstElement(final_set));
}

/*  
THIS MOD IS NOT MADE, DISTRIBUTED, OR SUPPORTED BY INFOGRAMES
OR PARTICLE SYSTEMS LTD. 

ELEMENTS TM & (C) INFOGRAMES AND PARTICLE SYSTEMS LTD.
*/