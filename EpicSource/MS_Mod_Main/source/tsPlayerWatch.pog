//*******************************************************************************************/
//
// tsPlayerWatch.pog
//
// This module follows the player and wakes nearby objects before player arrival.
// It also handles watching for daily updates. Music. And a good place for anything
// else that needs to be checked over every second or so.
// 
//
// 
//	
// 
//
// August 2003, Jon Freise
//******************************************************************************************/

package tsPlayerWatch;

//[of]:Imports
uses	Sim,
	iShip,
	iSim,
	Debug,
	Global,
	GUI,
	iGame,
	iGUI,
	iHUD,
	iHabitat,
	iMapEntity,
	iLagrangePoint,
	iFaction,
	iAI,
	INIFile,
	Input,
	List,
	Object,
	String,
	Task,
	State,
	Text,
	iUtilities,
	iCargo,
	iInventory,
	iComms,
	iRegion,
	Math,
	iPilotSetup,
	iMusic,
	iMod,
	iShipCreation,
	iDockport,
	UniGUI,
	tsTime,
	tsLCenter,
	tsFaction,
	tsFormation,
	tsPlayerFleetUpkeep,
	tsMusic,
	tsReputationWatch,
	tsLocationInit,
	tsCapsuleJump;

/*
      tsMail,
      tsShipMail,
      tBaseNet,
      tsNewsNet,
      tEAction,
*/
//[cf]


//[of]:Exports
provides	PlayerWatch,
		SystemEnter;
//[cf]


// Prototypes
prototype task location_watch(hmapentity location);
prototype ldsi_drop(hship player);
prototype task ldsi_watch(hisim waypoint);
prototype arrive_save(hmapentity location);


// The actual task that gets detached to watch the player with a 1 second test cycle.
// This task runs 'forever' unless it is killed by an external action.
//[of]:task watch_task()
task watch_task()
{
   hship player = iShip.FindPlayerShip();
   hisim target;
   set nearby_stations;
   int total, i;
   string system, prior_system, saved_system;
   list location_list;

   hmapentity location;
   hmapentity near_location;
   hmapentity near_lpoint;

   string prior_location;
   string saved_location;

   int current_day = 0;
   int two_counter = 0;

   bool docked = false;
   bool jumping = false;
   int save_count = 0;

   hsim nearest_station, station;
   hfaction faction;

   string order_name;
   hisim order_target;


   debug Debug.PrintString("Epic Action: Player watch active\n");

   current_day = tsTime.GetDay();

   while (1) {

      if (!tsCapsuleJump.IsCapsuleJumping()) {

         /*
         debug atomic {
            Debug.PrintString("player watch\n");
         }
         */

      ++two_counter;
      player = iShip.FindPlayerShip();
      system = iSim.ActiveWorld();

      // Check if the player is near any stations or lpoints
      nearby_stations = iMapEntity.SystemLagrangePoints();
      near_lpoint = iLagrangePoint.Nearest(iMapEntity.SystemLagrangePoints(), player);

      Set.Union(nearby_stations, iMapEntity.SystemHabitats());

      nearby_stations = iSim.SimsInRadiusFromSet(nearby_stations, 
                                                 player, 
                                                 Global.Float("gt_distance_culling"), 
                                                 T_Station|T_LagrangePoint);

      nearest_station = iHabitat.Nearest(nearby_stations, player);


      location_list = List.FromSet(nearby_stations);
      total = List.ItemCount(location_list);

      for (i=0;i<total;++i) {
         location = iMapEntity.Cast(List.GetNth(location_list,i));

         // If the station has already been culled, we 
         // restart the objects around the station.
         if (!tsLCenter.GetCullMarker(iSim.Cast(location))) {
            debug {
               Debug.PrintString(" PlayerWatch: Waking ");
               Debug.PrintHandle(location);
               Debug.PrintString(" \n");               
            }

			// activates all the appropriate activity for the location
			// the player is arriving at.
            tsLocationInit.Arrives(location);

            // Save if we have approached at least one station. 
            // This protects from saving directly after a restore.
            if (save_count > 0 ){
               arrive_save(location);
            }
            ++save_count;

            Task.Detach(start location_watch(location));
         }

      }//For each station less than culling distance away, check if warned.

      // Check player docking status.
      if (iSim.IsDockedToStructure(player, iSim.Cast(nearest_station)) ) {
         if (!docked) {
            debug Debug.PrintString("EPlayerWatch player just docked to station\n");
            // player just docked, report
            Task.Call("tsMain.PlayerDocked");
            docked = true;

            // Unhide the faction where the player has docked.
            faction = iSim.Faction(iSim.Cast(nearest_station));
            tsFaction.SetKnown(faction, true);
         }
      } else {
         if (docked) {
            debug Debug.PrintString("EPlayerWatch player just docked to station\n");
            // player just undocked, report.
            Task.Call("tsMain.PlayerUndocked");
            docked = false;
         }
      }

      // Check if the player has been shooting LDSI missiles. Create the fields and timers.
      ldsi_drop(player);

      if (two_counter > 1) {
         two_counter = 0;

         // Checks if the player or his fleet have been attacking factions.
         // updates faction standing to match.
         // tsReputationWatch.CheckForAttacks();

         tsMusic.SoundNear();

         // Check if a day has passed. Check all waiting actions for update.
         if (tsTime.GetDay() > current_day) {
            current_day = tsTime.GetDay();
            // Tell all other scripts that the day has passed.
            Task.Call("tsMain.DayUpdate");
         }
      }// every 2 seconds
      }//If not capsule jumping.

      Task.Sleep(Task.Current(), 1.0);
   }// While forever
}
//[cf]


// This task watches the player and takes action when the player has traveled
// more than the culling distance away from the identified location. When the
// player is no longer near to 'location' then tsLocationInit.Leaves is called
// and the task exits.
//[of]:task location_watch(hmapentity location)
task location_watch(hmapentity location)
{
   hship player;
   bool near = true;
   string name;

   do {

      Task.Sleep(Task.Current(), 1.0);

      if (tsCapsuleJump.IsCapsuleJumping()) {
         near = false;
      }else {
         player = iShip.FindPlayerShip();
         if (Global.Float("gt_distance_culling") < Sim.DistanceBetween(player, Sim.Cast(location))) {
            near = false;
         }
      }

   } while (near);


   debug{
      Debug.PrintString("PlayerWatch leaving ");
      Debug.PrintHandle(location);
      Debug.PrintString("\n");
   }





   tsLocationInit.Leaves(location);
}
//[cf]


// Call at final setup. Starts the 'watch_task' task
//[of]:PlayerWatch()
PlayerWatch()
{
   Task.Detach(start watch_task());
}
//[cf]


// Used to track when the player has jumped to a new system.
//[of]:SystemEnter()
SystemEnter()
{
   string prior_world, current_world;
   hship player;
   float fuel;

   current_world = iSim.ActiveWorld();

   if (!Global.Exists("gt_fleet_update_system")) {
      Global.CreateString("gt_fleet_update_system",GA_Write, current_world);

      debug Debug.PrintString("JumpWatch: Startingup\n");
   } else {
      prior_world = Global.String("gt_fleet_update_system");

      if (prior_world != current_world) {
         debug Debug.PrintString("JumpWatch: Player changed systems\n");
         Global.SetString("gt_fleet_update_system", current_world);

         if (!tsCapsuleJump.IsCapsuleSpace(current_world)){
            // We advance the game clock one full day for the jump.
            // But only on exiting capsule space.
            tsTime.AdvanceTime(Global.Int("gt_time_one_day"));
         }else{
            debug Debug.PrintString("JumpWatch: Player in capsule space.\n");
         }

      } else {
         debug Debug.PrintString("JumpWatch: Player arrived back in old system\n");
      }
   }
}
//[cf]


// Search around the player for LDSI weapon effects.
//[of]:ldsi_drop(hship player)
ldsi_drop(hship player)
{
   list waypoints;
   int i, total;
   hisim waypoint;

   waypoints = List.FromSet(iSim.SimsInRadius(player, 400km, T_Waypoint));

   total = List.ItemCount(waypoints);
   for (i=0;i<total;++i) {
      waypoint = iSim.Cast(List.GetNth(waypoints,i));

      /*
      debug {
         Debug.PrintHandle(waypoint);
         Debug.PrintString(" possible LDSI\n");
      }
      */

      if ("LDSI Marker" == Sim.Name(waypoint)) {
         if (!Object.PropertyExists(waypoint,"gt_field_handle")) {
            Object.AddHandleProperty(waypoint, "gt_field_handle", none);
            Task.Detach(start ldsi_watch(waypoint));
         }
      }
   }
}
//[cf]


//[of]:task ldsi_watch(hisim waypoint)
task ldsi_watch(hisim waypoint)
{
   int timer = 100;
   hregion ldsi;

   /*
   debug {
      Debug.PrintHandle(waypoint);
      Debug.PrintString("\n");
   }
   */

   ldsi = iRegion.CreateLDSI(waypoint, 30km);

   while (1) {
      Task.Sleep(Task.Current(), 1.0);
      --timer;
      if (timer < 0) {
         iRegion.Destroy(ldsi);
         Sim.Destroy(waypoint);
         return;
      }
   }
}
//[cf]


//[of]:arrive_save(hmapentity location)
arrive_save(hmapentity location)
{
   string name;

   if (T_LagrangePoint != iSim.Type(location)) {
      // Do an autosave when the player leaves a station.

      if (Global.Bool("gt_option_autosave_on")) {

         iHUD.SetPrompt("SAVING GAME", "APPROACH STATION SAVE");

         name = tsTime.TimeToString();
         name = String.Join(name,"  ");
         name = String.Join(name,Sim.Name(location));
         name = String.Join(name,"  ");
         name = String.Join(name,Text.Field(iSim.ActiveWorld(), FT_Text));

         atomic {
            Task.Call("tsMain.SaveGame");
         }

         iGame.SaveGame( 1, String.Join(name, " Arrive Station" ));   

         iHUD.SetPrompt("", "");
      }
   }
}
//[cf]

/*  
THIS MOD IS NOT MADE, DISTRIBUTED, OR SUPPORTED BY INFOGRAMES
OR PARTICLE SYSTEMS LTD. 

ELEMENTS TM & (C) INFOGRAMES AND PARTICLE SYSTEMS LTD.
*/
